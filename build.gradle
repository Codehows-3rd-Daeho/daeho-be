plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.codehows'
version = '0.0.1-SNAPSHOT'
description = 'daeho-be'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // WebFlux 제거 - RestClient로 마이그레이션 완료 (동기식 HTTP 클라이언트)
    // implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    //Monitor
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    // JWT
    implementation("io.jsonwebtoken:jjwt-api:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.5")
    // Kafka (Removed for lightweight architecture)
    // implementation 'org.springframework.kafka:spring-kafka'
    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    // Spring Retry
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects'
    // Resilience4j Circuit Breaker
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
    // Web Push
    implementation 'nl.martijndwars:web-push:5.1.1'
    implementation 'org.apache.httpcomponents:httpclient' // web-push를 위한 http client
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1' // web-push를 위한 암호화
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    // reactor-test 제거 - WebFlux/Reactor 더 이상 사용하지 않음
    // testImplementation 'io.projectreactor:reactor-test'
    // testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher' // spring-kafka-test 제거로 인해 명시적 추가 필요
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.12' // Mockito 호환성을 위한 Byte Buddy 버전 명시

    // Testcontainers
    testImplementation 'org.testcontainers:testcontainers:1.19.7'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
    testImplementation 'org.testcontainers:mysql:1.19.7'
    // testImplementation 'org.testcontainers:kafka:1.19.7'
    testImplementation 'com.redis:testcontainers-redis:2.2.0'

    // WireMock (외부 API Mock)
    testImplementation 'org.wiremock:wiremock-standalone:3.4.2'

    // Awaitility (비동기 테스트)
    testImplementation 'org.awaitility:awaitility:4.2.1'

    // JMH (Java Microbenchmark Harness) - 성능 벤치마크
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // test 작업이 끝나면 jacocoTestReport 자동 실행
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = "0.8.12" // JaCoCo 버전 명시
}

jacocoTestReport {
    dependsOn test // jacocoTestReport는 test 작업에 의존
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // 제외할 클래스/패키지 패턴
                    '**/DaehoBeApplication.class',
                    '**/*Config.*',
                    '**/config/**/*.*',
                    '**/jwtAuth/**/*.*',
                    '**/constant/**/*.*',
                    '**/dto/**/*.*',
                    '**/entity/**/*.*',
                    '**/common/entity/*.*'
            ])
        }))
    }
}
